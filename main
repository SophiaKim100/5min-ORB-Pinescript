//@version=6
strategy("5min ORB", overlay=true, initial_capital = 1000, commission_type=strategy.commission.percent, commission_value=0.1, slippage=2)
import TradingView/ta/9

// User Inputs
startDate = timestamp(2022, 1, 1, 9, 30)
endDate = timestamp(2026, 12, 31, 16, 00)
timeframeMinutes = input(5, "Opening Range Minutes")
exitEOD = input(false, "Exit End of Day")

// Define session timestamps
marketOpenHour = 9
marketOpenMinute = 30
marketCloseHour = 16
marketCloseMinute = 00
latestEntryHour = 11
latestEntryMinute = 00
premarketOpen = 4

// Define ORB, PM, and PD levels
var float openingRangeHigh = na 
var float openingRangeLow = na 
var bool ORBrangeSet = false

var float breakoutCandleHigh = na 
var float breakoutCandleLow = na
var int breakoutBarIndex = na 

var float premarketHigh = na
var float premarketLow = na
var bool PMrangeSet = false

var float prevHigh = na
var float prevLow = na
var bool PDrangeSet = false

// Detect New Day and Reset ORB
isNewDay = ta.change(dayofmonth(time))  != 0   // True when a new day starts
if isNewDay
    openingRangeHigh := na
    openingRangeLow := na
    ORBrangeSet := false
    PMrangeSet := false
    PDrangeSet := false
    breakoutCandleHigh := na
    breakoutCandleLow := na
    breakoutBarIndex := na 
    premarketHigh := na
    premarketLow := na

// Check if the day has changed for PD levels
if (dayofweek != dayofweek[1])
    prevHigh := request.security(syminfo.tickerid, "D", high[0])  // Highest price from previous session
    prevLow := request.security(syminfo.tickerid, "D", low[0])   // Lowest price from previous session

if (hour == marketCloseHour and minute == marketCloseMinute)
    PDrangeSet := true

// Update premarket high and low
if (not PMrangeSet and hour >= premarketOpen and hour <= marketOpenHour and minute <= marketOpenMinute)
    premarketHigh := na(premarketHigh) ? high : math.max(premarketHigh, high)
    premarketLow := na(premarketLow) ? low : math.min(premarketLow, low)

if (hour == marketOpenHour and minute == marketOpenMinute)
    PMrangeSet := true

// Capture opening Range 
if (not ORBrangeSet and hour == marketOpenHour and minute >= marketOpenMinute and minute < (marketOpenMinute + timeframeMinutes))
    openingRangeHigh := na(openingRangeHigh) ? high : math.max(openingRangeHigh, high)
    openingRangeLow := na(openingRangeLow) ? low : math.min(openingRangeLow, low)

//Lock the range after 5min
if (hour == marketOpenHour and minute == marketOpenMinute + timeframeMinutes)
    ORBrangeSet := true

// Define Strong Retest Candle Types
candleRange = high - low
candleBody = math.abs(close - open)
upperWick = high - math.max(open, close)
lowerWick = math.min(open, close) - low

//Hammer/Inverted Hammer (Long Wick Candles)
isHammerBullish = lowerWick >= candleBody * 1.5  // Long lower wick, small body (bullish)
isInvertedHammerBearish = upperWick >= candleBody * 1.5  // Long upper wick, small body (bearish)

//Large Body Candles
isStrongBullishCandle = candleBody > candleRange * 0.6  // Bullish candle with >60% body
isStrongBearishCandle = candleBody > candleRange * 0.6  // Bearish candle with >60% body

// Entry Conditions
breakoutLong = ta.crossover(close, openingRangeHigh) and isStrongBullishCandle
breakoutShort = ta.crossunder(close, openingRangeLow) and isStrongBearishCandle
validTime = (hour >= marketOpenHour and hour <= latestEntryHour and (hour != latestEntryHour or minute <= latestEntryMinute))

// Store Breakout Candle High/Low
if breakoutLong or breakoutShort
    breakoutCandleHigh := high
    breakoutCandleLow := low
    breakoutBarIndex := bar_index

//Retest Must Come After breakout bar 
retestCandleAfterBreakout = bar_index > breakoutBarIndex

//Valid Retests
validRetestUp = retestCandleAfterBreakout and low <= openingRangeHigh and close > openingRangeHigh and (isHammerBullish or isStrongBullishCandle)
validRetestDown = retestCandleAfterBreakout and high >= openingRangeLow and close < openingRangeLow and (isInvertedHammerBearish or isStrongBearishCandle)

// Stop-Loss with Close Confirmation
var float stopLossLong = na
var float stopLossShort = na 
var float takeProfit1Long = na
var float takeProfit2Long = na
var float takeProfit1Short = na
var float takeProfit2Short = na 

//Reset daily high/low at start of each day
var float dailyHigh = na
var float dailyLow = na 

if ta.change(dayofmonth(time))  != 0
    dailyHigh := high
    dailyLow := low

//Variables for TP and SL lines
var line longTP1Line = na 
var line longSLLine = na 
var line shortTP1Line = na 
var line shortSLLine = na

// Long Trade Execution
if (validRetestUp and validTime and time >= startDate and time <= endDate)
    strategy.entry("Long", strategy.long)
    stopLossLong := breakoutCandleLow // Use breakout candle low
    rrTargetLong = close + (close - stopLossLong) * 2 //1:2 RR Target 

    // TP1 = First to hit newHighOfDay or 1:2 RR 
    takeProfit1Long := rrTargetLong
   

    //Plot TP and SL lines
    longTP1Line := line.new(x1=bar_index, y1=takeProfit1Long, x2=bar_index + 1, y2=takeProfit1Long, color=color.green, width = 2, style=line.style_dashed)
    longSLLine := line.new(x1=bar_index, y1=stopLossLong, x2=bar_index + 1, y2=stopLossLong, color=color.red, width = 2, style=line.style_dashed)

    strategy.exit("1", from_entry = "Long", qty_percent = 100, limit = takeProfit1Long)

//Short Trade Execution
if (validRetestDown and validTime and time >= startDate and time <= endDate)
    strategy.entry("Short", strategy.short)

    stopLossShort := breakoutCandleHigh
    rrTargetShort = close - (stopLossShort - close) * 2  // 1:2 RR target

    // TP1 = First to hit New Low of Day or 1:2 RR
    takeProfit1Short := rrTargetShort

    //Plot TP and SL lines
    shortTP1Line := line.new(x1=bar_index, y1=takeProfit1Short, x2=bar_index + 1, y2=takeProfit1Short, color=color.green, width = 1, style=line.style_dashed)
    shortSLLine := line.new(x1=bar_index, y1=stopLossShort, x2=bar_index + 1, y2=stopLossShort, color=color.red, width = 1, style=line.style_dashed)

    strategy.exit("1", from_entry="Short", qty_percent=100, limit=takeProfit1Short)


//Close Trade if a Candle Fully Closes Beyond Stop Loss
if strategy.position_size > 0 and close < breakoutCandleLow
    strategy.close("Long", comment="SL Hit - Long")

if strategy.position_size < 0 and close > breakoutCandleHigh
    strategy.close("Short", comment="SL Hit - Short")


// Exit at End of Day
if (exitEOD and hour == marketCloseHour and minute >= marketCloseMinute)
    strategy.close("Long", comment = "EOD Close")
    strategy.close("Short", comment = "EOD Close")

// Preserve Visuals
plot(openingRangeHigh, color=color.green, title="Opening Range High")
plot(openingRangeLow, color=color.red, title="Opening Range Low")
plot(premarketHigh, "Premarket High", color=color.blue, linewidth=1, style=plot.style_line)
plot(premarketLow, "Premarket Low", color=color.blue, linewidth=1, style=plot.style_line)
plot(prevHigh, "Previous Day High", color = color.purple, linewidth = 1, style = plot.style_line)
plot(prevLow, "Previous Day Low", color = color.purple, linewidth = 1, style = plot.style_line)
